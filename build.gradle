/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor Quick-start components'

ext {
	gradleVersion = '1.7-rc-1'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Languages
	groovyVersion = '2.1.5'

	// Logging
	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.13'

	// Jetty
	jettyVersion = '9.0.1.v20130408'

  // Reactor
  reactorVersion = '1.0.0.BUILD-SNAPSHOT'

	// Testing
	spockVersion = '0.7-groovy-2.0'
}

apply from: "$gradleScriptDir/setup.gradle"

allprojects {
	apply plugin: 'idea'

	configurations.all {
		exclude group: 'commons-logging'
		exclude module: 'junit'
	}

	group = 'org.projectreactor.quickstart'
	repositories {
		mavenLocal()
    mavenCentral()
		maven { url 'http://repo.springsource.org/libs-release' }
		maven { url 'http://repo.springsource.org/libs-snapshot' }
	}
}

subprojects { subproject ->
	apply plugin: 'java'
	apply plugin: 'eclipse'

	sourceCompatibility = 7
	targetCompatibility = 7

	[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:none', '-g']

	// dependencies that are common across all java projects
	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"
		runtime "ch.qos.logback:logback-classic:$logbackVersion"

		// Reactor
		compile "org.projectreactor:reactor-core:$reactorVersion"

		// Testing
		testCompile 'junit:junit-dep:4.11'
		testCompile 'org.hamcrest:hamcrest-library:1.3'
	}

}

project('core') {
	description = 'Reactor quickstart core components'
	dependencies {
	}
}

project('simple') {
	description = 'Reactor quickstart simple event handling example'
	dependencies {
		compile project(':core')
	}
}

project('composable') {
	description = 'Reactor quickstart Composable example'
	dependencies {
		compile project(':core')
	}
}

project('groovy') {
	description = 'Reactor quickstart Groovy example'
	dependencies {
		compile project(':core')

		compile "org.projectreactor:reactor-groovy:$reactorVersion"
	}
}

project('websocket') {
	description = 'Reactor quick-start websocket example'
	dependencies {
		compile project(':core')

		// WebSocket support
		compile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
		compile "org.eclipse.jetty.websocket:websocket-server:$jettyVersion"
		compile("javax.net.websocket:javax.net.websocket-api:1.0-b08", provided)
	}
}



